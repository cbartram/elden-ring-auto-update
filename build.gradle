plugins {
  id 'java'
  id 'application'
  id 'org.javamodularity.moduleplugin' version '1.8.12'
  id 'org.openjfx.javafxplugin' version '0.0.13'
  id 'org.beryx.jlink' version '2.25.0'
  id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group 'com.elden.ring.mod'
version '1.0-SNAPSHOT'

repositories {
  mavenCentral()
}

ext {
  junitVersion = '5.10.0'
}

sourceCompatibility = '11'
targetCompatibility = '11'

tasks.withType(JavaCompile) {
  options.encoding = 'UTF-8'
}

application {
  mainModule = 'com.elden.ring.mod.manager'
  mainClass = 'com.elden.ring.mod.manager.AutoUpdaterApplication'
}

javafx {
  version = '17.0.6'
  modules = ['javafx.controls', 'javafx.fxml']
}

dependencies {
  implementation('org.controlsfx:controlsfx:11.1.2')
  implementation('org.kordamp.bootstrapfx:bootstrapfx-core:0.4.0')

  testImplementation("org.junit.jupiter:junit-jupiter-api:${junitVersion}")
  testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junitVersion}")

  compileOnly 'org.projectlombok:lombok:1.18.34'
  annotationProcessor 'org.projectlombok:lombok:1.18.34'

  testCompileOnly 'org.projectlombok:lombok:1.18.34'
  testAnnotationProcessor 'org.projectlombok:lombok:1.18.34'

  implementation 'org.apache.logging.log4j:log4j-api:2.23.1'
  implementation 'org.apache.logging.log4j:log4j-core:2.23.1'

  implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.17.2'
}

test {
useJUnitPlatform()}

jlink {
  imageZip = project.file("${buildDir}/distributions/app-${javafx.platform.classifier}.zip")
  options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
  launcher {
    name = 'app'
  }
}

jlinkZip {
  group = 'distribution'
}

// Add this block for creating the fat JAR
shadowJar {
  manifest {
    attributes 'Main-Class': application.mainClass
  }
  mergeServiceFiles()
  archiveClassifier.set('fat')
}

// Add this task to copy JavaFX dependencies into the fat JAR
task copyJavaFXDependencies(type: Copy) {
  from configurations.runtimeClasspath
  into "build/libs/javafx-deps"
  include "javafx*.jar"
}

// Make shadowJar depend on copyJavaFXDependencies
shadowJar.dependsOn copyJavaFXDependencies

// Modify the run task to use the fat JAR
tasks.named('run') {
  dependsOn shadowJar
  doFirst {
    jvmArgs = [
            '--module-path', 'build/libs/javafx-deps',
            '--add-modules', 'javafx.controls,javafx.fxml'
    ]
    classpath = files("build/libs/ersc-manager-${project.version}.jar")
  }
}